/**
 * ResearchSpace
 * Copyright (C) 2020, Â© Trustees of the British Museum
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    // Apply the war plugin to add support for Java webpapp build
    id 'war'

    // To run app in the development mode with jetty container
    id 'org.gretty' version '3.0.3'

    // To run webpack-dev-server in the background
    id 'com.github.psxpaul.execfork' version '0.1.13'

    // To generate eclipse project files
    id 'eclipse'
}


// currently we use Java 8 for development, but ResearchSpace can be run fine on later Java versions
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


clean {
    delete "${projectDir}/src/main/webpapp/assets"
    delete "${projectDir}/node_modules"
}

// set default formatter for eclipse project generated by gradle
def eclipseJdtProps = new Properties()
file("${projectDir}/.eclipse/org.eclipse.jdt.core.prefs").withInputStream {
    eclipseJdtProps.load(it)
}
eclipse {
    jdt {
        file {
            withProperties { properties ->
                properties.putAll(eclipseJdtProps)
            }
        }
    }
}

// configuration for war packaging
war {
    from 'COPYING'
    from 'COPYING.GPLv3'
}


farms {
    farm 'researchspace', {
        webapp project,
            contextPath: '/'

        // setup blazegraph for local development
        webapp 'com.blazegraph:blazegraph-war:2.2.0-20160908.003514-6',
            contextPath: '/blazegraph'

        // setup digilib for local development
        webapp 'digilib:digilib-webapp:2.9-20200421.092548-16',
            contextPath: '/digilib',
            initParameters: ["config-file": "./bundle-config/digilib-config.xml"]

    }
}

products {
    product 'researchspace', {}
}

// configuration for local jetty container
gretty {
    // see actual jetty version in gradle.properties
    servletContainer = 'jetty9.4'
    httpPort = 10214
    contextPath = '/'

    // to serve web assets directly from src/main/webpapp folder
    // see https://akhikhl.github.io/gretty-doc/Gretty-configuration.html#_inplacemode
    // see https://akhikhl.github.io/gretty-doc/Fast-reload.html
    inplaceMode = 'hard'

    loggingLevel='DEBUG'

    // don't restart jetty when templates in the resource folder are changed
    fastReload = "${projectDir.getAbsolutePath()}/src/main/resources/org/researchspace/apps".toString()

    // no need to write logs into file in development mode
    fileLogEnabled = false

    // use custom jetty-env.xml to disable websocket support
    contextConfigFile = "${projectDir}/jetty-env.xml"

    if (!project.gradle.startParameter.taskNames.contains('buildZip')) {
        jvmArgs = [
            // blazegraph options, for runAll
            "-Dcom.bigdata.rdf.sail.webapp.ConfigParams.propertyFile=${projectDir}/bundle-config/RWStore.properties",

            // for properties that get value from project.properties see gradle.properties file for more details
            "-Dconfig.environment.sparqlEndpoint=${project.properties['sparqlEndpoint']}",
            "-Dconfig.proxy.IIIF.targetUri=${project.properties['iiifScaler']}",

            // delopment storages for image upload functionality
            "-Dconfig.storage.images.type=nonVersionedFile",
            "-Dconfig.storage.images.mutable=true",
            "-Dconfig.storage.images.root=${project.properties['imageStorageRoot']}",

            // tmp storage is needed for image upload functionality
            "-Dconfig.storage.tmp.type=nonVersionedFile",
            "-Dconfig.storage.tmp.mutable=true",
            "-Dconfig.storage.tmp.root=${java.nio.file.Files.createTempDirectory('images-tmp')}",

            // set development mode to true to make sure that assets are always up to date
            // see bindings for AssetsMapProvider in MainGuiceModule.java
            "-Dconfig.global.isDevelopmentMode=true",

            "-DruntimeDirectory=${projectDir.getAbsolutePath()}/runtime-data",

            // sets the location to the config folder to a temporary location for development
            "-Dcom.metaphacts.config.baselocation=${projectDir.getAbsolutePath()}/runtime-data/config",

            // make apps writable in local development mode
            '-Dconfig.mutablePluginApps=true',

            // setup help, admin and assets storage in development mutable mode,
            // in production they are available from immutable classpath storage,
            // see src/main/resources/apps/internalStorage.prop
            '-Dconfig.storage.help.type=nonVersionedFile',
            '-Dconfig.storage.help.mutable=true',
            "-Dconfig.storage.help.root=${projectDir.getAbsolutePath()}/src/main/resources/org/researchspace/apps/help",
            '-Dconfig.storage.admin.type=nonVersionedFile',
            '-Dconfig.storage.admin.mutable=true',
            "-Dconfig.storage.admin.root=${projectDir.getAbsolutePath()}/src/main/resources/org/researchspace/apps/admin",
            '-Dconfig.storage.assets.type=nonVersionedFile',
            '-Dconfig.storage.assets.mutable=true',
            "-Dconfig.storage.assets.root=${projectDir.getAbsolutePath()}/src/main/resources/org/researchspace/apps/assets",

            // You may change the log4j settings by switching to one of the predefined log4j2 config files,
            // see /src/main/webapp/etc for options (descriptions of log behavior is given in the files).
            // - Development log files: log4j2-debug.xml, log4j2-trace.xml, log4j2-trace2.xml
            // - Production log files: log4j2.xml, log4j2-debug.xml
            "-Dlog4j.configurationFile=file://${projectDir.getAbsolutePath()}/src/main/webapp/etc/${findProperty('logLevel')}.xml",

            "-Dconfig.environment.shiroConfig=${projectDir.getAbsolutePath()}/src/main/resources/org/researchspace/apps/assets/config/shiro.ini",
        ]
    }
}


// install npm dependencies in development mode
task npmInstall(type: Exec) {
    executable 'npm'
    args 'i', '--no-audit', '--no-fund', '--loglevel=error'
}

// we want to get clean node_modules when we are building on CI or production version locally
// see https://docs.npmjs.com/cli/ci
task npmInstallCi(type: Exec) {
    executable 'npm'
    args 'ci', '--no-audit', '--no-fund', '--loglevel=error'
}

// execute webpack in production mode with minifications, etc
task runProdWebpack(type: Exec) {
    executable 'npm'
    args 'run', 'prod'
}
tasks.runProdWebpack.dependsOn npmInstallCi

// task to execute webpack in watch mode, see ./webpack/package.json
task startWebpack(type: com.github.psxpaul.task.ExecFork) {
    executable = 'npm'
    args = [ 'run', 'dev' ]
    timeout = 1200

    // wait for message produces by webpack in minimal log mode
    waitForOutput = 'modules'
}
tasks.startWebpack.dependsOn npmInstall

// define custom start task so we have more convenient names than
// farmRunresearchspace, appRun, farmRunDebugresearchspace, appRunDebug, archiveProductresearchspace
task runAll
task run
task debugAll
task debug
task buildZip

// here we connect our custom tasks to builtins
// see https://stackoverflow.com/a/33960441 on why project.afterEvaluate is needed
project.afterEvaluate {
    // run production webpack build if we are building war package
    tasks.war.dependsOn runProdWebpack

    // run webpack in dev mode if we are running local gretty task
    tasks.prepareInplaceWebApp.dependsOn startWebpack

    // connect our custom tasks with tasks from gretty
    tasks.debugAll.dependsOn tasks.farmRunDebugresearchspace
    tasks.debug.dependsOn tasks.appRunDebug
    tasks.runAll.dependsOn tasks.farmRunresearchspace
    tasks.run.dependsOn tasks.appRun
    tasks.buildZip.dependsOn tasks.archiveProductresearchspace
}

// execute frontend tests
task testKarma(type: Exec) {
    workingDir "$projectDir/webpack"
    executable 'npm'
    args 'run', 'test-ci'
}
tasks.testKarma.dependsOn npmInstall
test.dependsOn testKarma

repositories {
    // Use jcenter for resolving dependencies.
    jcenter()

    // for blazegraph war
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

    // for digilib war
    maven { url 'https://robcast.github.io/digilib-repo/maven-repo' }

    // temp, until new version of smiley-http-proxy-servlet is released
    maven {
        url 'https://dl.bintray.com/researchspace/public'
    }
}

ext {
    RDF4J_VERSION = '3.2.0'
    LOG4J_VERSION = '2.13.1'
    JERSEY_VERSION = '2.30.1'
    JACKSON_VERSION = '2.10.3'
    GUICE_VERSION = '4.2.3'
    SHIRO_VERSION = '1.5.2'
    IMAGEIO_VERSION = '3.5'
}

dependencies {
    compileOnly (
        "javax.servlet:javax.servlet-api:3.1.0",
        "javax.ws.rs:javax.ws.rs-api:2.0.1",

        // add jetty as compile dependency so we get sources and javadocs and can debug it locally
        'org.eclipse.jetty:jetty-server:9.4.28.v20200408'
    )
    implementation (
        // RDF4J dependencies
        "org.eclipse.rdf4j:rdf4j-repository-manager:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-repository-sail:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-repository-sparql:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-repository-http:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-sail-nativerdf:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-sail-memory:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-sail-federation:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-api:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-binary:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-jsonld:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-n3:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-nquads:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-ntriples:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-rdfjson:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-rdfxml:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-trig:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-trix:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-rio-turtle:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-model:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-query:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-queryalgebra-model:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-util:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-queryrender:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-queryresultio-sparqljson:${RDF4J_VERSION}",
        "org.eclipse.rdf4j:rdf4j-queryresultio-text:${RDF4J_VERSION}",
        "com.github.jsonld-java:jsonld-java:0.12.0",

        // logging related dependencies
        "org.apache.logging.log4j:log4j-api:${LOG4J_VERSION}",
        "org.apache.logging.log4j:log4j-core:${LOG4J_VERSION}",
        "org.apache.logging.log4j:log4j-web:${LOG4J_VERSION}",
        "org.apache.logging.log4j:log4j-1.2-api:${LOG4J_VERSION}",
        "org.apache.logging.log4j:log4j-slf4j-impl:${LOG4J_VERSION}",
        "org.apache.logging.log4j:log4j-jul:${LOG4J_VERSION}",
        "org.apache.logging.log4j:log4j-jcl:${LOG4J_VERSION}",

        // Jersey, JAX-RS implementation
        "org.glassfish.jersey.core:jersey-server:${JERSEY_VERSION}",
        "org.glassfish.jersey.containers:jersey-container-servlet:${JERSEY_VERSION}",
        "org.glassfish.jersey.media:jersey-media-json-jackson:${JERSEY_VERSION}",
        "org.glassfish.jersey.media:jersey-media-multipart:${JERSEY_VERSION}",
        "org.glassfish.jersey.inject:jersey-hk2:${JERSEY_VERSION}",

        // Jackson, for JSON handling
        "com.fasterxml.jackson.core:jackson-core:${JACKSON_VERSION}",
        "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VERSION}",
        "com.fasterxml.jackson.core:jackson-annotations:${JACKSON_VERSION}",
        "com.fasterxml.jackson.jaxrs:jackson-jaxrs-base:${JACKSON_VERSION}",
        "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${JACKSON_VERSION}",

        // Google Guice, for dependency injection
        "com.google.inject:guice:${GUICE_VERSION}",
        "com.google.inject.extensions:guice-servlet:${GUICE_VERSION}",

        //jersey-guice integration
        "org.glassfish.hk2:guice-bridge:2.6.1",


        "io.buji:buji-pac4j:4.0.0",
        "org.pac4j:pac4j-oauth:3.1.0",
        "org.pac4j:pac4j-saml:3.1.0",


        // Apache commons stuff
        "org.apache.commons:commons-compress:1.20",
        "org.apache.commons:commons-lang3:3.10",

        // configuration
        "org.apache.commons:commons-configuration2:2.7",
        "commons-beanutils:commons-beanutils:1.9.4",

        //guava
        "com.google.guava:guava:28.2-jre",


        //security
        "org.apache.shiro:shiro-core:${SHIRO_VERSION}",
        "org.apache.shiro:shiro-web:${SHIRO_VERSION}",
        "org.apache.shiro:shiro-guice:${SHIRO_VERSION}",
        "org.apache.shiro:shiro-jaxrs:${SHIRO_VERSION}",

        //templates
        "com.github.jknack:handlebars:4.2.0",

        //semantic versioning
        "com.github.zafarkhaja:java-semver:0.9.0",

        // java classes and resources reflection, i.e. annotation scanning on classpath
        "io.github.classgraph:classgraph:4.8.68",

        //http core and client, successor of and replacement for Commons HttpClient 3.x
        "org.apache.httpcomponents:httpclient:4.5.12",
        "org.apache.httpcomponents:httpcore:4.4.13",

        // extracting the prefered mime type, for example, sparql servlet
        "org.commonjava.mimeparse:mimeparse:0.1.3.3",

        //pf4j plugin framework
        "ro.fortsoft.pf4j:pf4j:1.3.0",

        "org.mitre.dsmiley.httpproxy:smiley-http-proxy-servlet:1.12.1",

        //JsonPath
        "com.jayway.jsonpath:json-path:2.4.0",

        // JGit for Git storage support
        "org.eclipse.jgit:org.eclipse.jgit:5.4.0.201906121030-r",

        "com.twelvemonkeys.imageio:imageio-tiff:${IMAGEIO_VERSION}",
        "com.twelvemonkeys.imageio:imageio-core:${IMAGEIO_VERSION}",
    )

    testImplementation (
        "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-inmemory:${JERSEY_VERSION}",
        "org.hamcrest:hamcrest-library:1.3",
        "org.hamcrest:hamcrest-core:1.3",
        "com.novocode:junit-interface:0.11",
        "org.mockito:mockito-core:1.9.5",
        "junit:junit:4.10",
        "com.github.sdorra:shiro-unit:1.0.1",
        "org.jukito:jukito:1.4.1",
        "org.skyscreamer:jsonassert:1.5.0"
    )
}
